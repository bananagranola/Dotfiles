# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Scytrin dai Kinthra <scytrin@gmail.com>

pkgname=st-git
epoch=1
pkgver=20130324
pkgrel=1
pkgdesc='Simple virtual terminal emulator for X'
url='http://git.suckless.org/st/'
license=('MIT')
depends=('libxext' 'libxft')
makedepends=('git' 'ncurses')
arch=('i686' 'x86_64' 'armv7h')
provides=('st')
conflicts=('st')
replaces=('st-hg')

source=('config.h')

_gitroot='http://git.suckless.org/st/'
_gitname='st'

build() {
	cd "${srcdir}"

	( cd "${_gitname}" && git pull origin ) || git clone "${_gitroot}" "${_gitname}"
	rm -fr build; cp -a "${_gitname}" build; cd build

	mv "${srcdir}/config.h" .

	make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
	cd "${srcdir}/build"

	make PREFIX=/usr DESTDIR="${pkgdir}" TERMINFO="${pkgdir}/usr/share/terminfo" install

	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
}
md5sums=('beeffffca37a3cbb32b45cb36ad82735')
