# Contributer: Andrea Scarpino <andrea@archlinux.org>
# Maintainer: Daniel YC Lin <dlin.tw at gmail>

pkgname=grive-git
pkgver=20121101
pkgrel=1
pkgdesc='Open source Linux client for Google Drive'
arch=('i686' 'x86_64')
url='http://www.lbreda.com/grive/start'
license=('GPL2')
depends=('json-c' 'curl' 'boost-libs' 'expat' 'libgcrypt')
makedepends=('cmake' 'git' 'boost')
provides=('grive')
conflicts=('grive' 'cppunit')

_gitroot="https://github.com/Grive/grive.git"
_gitname="grive"

build() {
  cd "${srcdir}"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "${srcdir}"/build
  mkdir "${srcdir}"/build
  cd "${srcdir}"/build
  cmake ../${_gitname} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  sed -i -e '/find_package(BFD)/d' "${srcdir}"/grive/libgrive/CMakeLists.txt
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
